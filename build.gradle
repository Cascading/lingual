/*
 * Copyright (c) 2007-2012 Concurrent, Inc. All Rights Reserved.
 *
 * Project and contact information: http://www.cascading.org/
 *
 * This file is part of the Cascading project.
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *     http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */



import java.text.SimpleDateFormat

apply plugin: 'java'
apply plugin: 'maven'
apply plugin: 'idea'

apply from: 'etc/providedCompile.gradle'

ext.cascadingVersion = '2.2.+'
ext.hadoopVersion = '1.0.4'

repositories {
  mavenLocal()
  mavenCentral()
  mavenRepo name: 'conjars', url: 'http://conjars.org/repo/'
  mavenRepo name: 'typesafe', url: 'http://repo.typesafe.com/typesafe/repo/' // sqlline
}

dependencies {
  compile group: 'cascading', name: 'cascading-core', version: cascadingVersion, changing: true
  compile group: 'cascading', name: 'cascading-local', version: cascadingVersion, changing: true
  compile group: 'cascading', name: 'cascading-hadoop', version: cascadingVersion, changing: true
  compile group: 'cascading', name: 'bind', version: '1.0-wip-dev', changing: true

  compile( group: 'net.hydromatic', name: 'optiq', version: '0.1-SNAPSHOT', changing: true ) {
    exclude group: 'xerces'
    exclude group: 'xalan'
  }

  compile group: 'net.hydromatic', name: 'linq4j', version: '0.1-SNAPSHOT', changing: true

//  compile group: 'sqlline', name: 'sqlline', version: '1_0_2'
  testCompile group: 'commons-io', name: 'commons-io', version: '2.1'

//  runtime group: 'jline', name: 'jline', version: '1.0'
  runtime group: 'org.slf4j', name: 'slf4j-log4j12', version: '1.6.1'
  runtime group: 'log4j', name: 'log4j', version: '1.2.16'

  testCompile group: 'junit', name: 'junit', version: '4.8.+'
  testCompile group: 'com.google.guava', name: 'guava', version: '13.0.1'

  testCompile group: 'cascading', name: 'cascading-core', version: cascadingVersion, classifier: 'tests', changing: true
  testCompile group: 'cascading', name: 'cascading-local', version: cascadingVersion, classifier: 'tests', changing: true
  testCompile group: 'cascading', name: 'cascading-hadoop', version: cascadingVersion, classifier: 'tests', changing: true
  testCompile group: 'cascading', name: 'cascading-platform', version: cascadingVersion, classifier: 'tests', changing: true

  // hadoop deps
  providedCompile( group: 'org.apache.hadoop', name: 'hadoop-core', version: hadoopVersion ) {
    exclude group: 'ant'
    exclude group: 'junit'
  }

  testCompile group: 'org.apache.hadoop', name: 'hadoop-test', version: hadoopVersion
  testCompile group: 'commons-io', name: 'commons-io', version: '2.1'

  testRuntime 'javax.ws.rs:jsr311-api:1.1.1' // missed dep in hadoop, go figure
}

test {
  include '**/*SqlTest.class'
}

task distCopy( type: Copy, dependsOn: jar ) {

  into "${buildDir}/dist/${archivesBaseName}"

  from 'README.md'
  from 'build.gradle'
  from( 'src' ) {into 'src'}

  from "$buildDir/libs"
}

task dist( type: Tar, dependsOn: distCopy ) {

  compression = "GZIP"
  classifier = new SimpleDateFormat( "yyyyMMdd" ).format( new Date() )

  from "${buildDir}/dist/"
}