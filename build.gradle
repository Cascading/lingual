/*
 * Copyright (c) 2007-2013 Concurrent, Inc. All Rights Reserved.
 *
 * Project and contact information: http://www.cascading.org/
 *
 * This file is part of the Cascading project.
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *     http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */

apply from: 'etc/version.gradle'
apply from: 'etc/s3Upload.gradle'
apply from: 'etc/dependencyVersions.gradle'
apply plugin: 'java'
apply plugin: 'idea'
apply plugin: 'eclipse'

ext.cascadingVersion = '2.2.0-+'

// set property to use locally installed cascading builds
// otherwise wip releases will be pulled from conjars
if( System.properties[ 'lingual.release.local' ] )
  cascadingVersion = '2.2.0-wip-dev'

buildscript {
  repositories {
    mavenCentral()
  }

  dependencies {
    classpath 'eu.appsatori:gradle-fatjar-plugin:0.2-rc1'
  }
}

subprojects {

  apply plugin: 'java'
  apply plugin: 'maven'
  apply plugin: 'idea'
  apply plugin: 'eclipse'

  // lingual is a sub-project of cascading
  // project name is set in settings.gradle
  group = 'cascading'
  version = releaseVersion

  apply from: "${rootDir}/etc/includes.gradle"
  apply from: "${rootDir}/etc/dependencyVersions.gradle"

  configurations {
    testArtifacts {
      extendsFrom testRuntime
    }
    jarjar
  }

  repositories {
    mavenLocal()
    mavenCentral()
    mavenRepo name: 'conjars', url: 'http://conjars.org/repo/'
    mavenRepo name: 'pentaho', url: 'http://repo.pentaho.org/artifactory/repo/' // optiq deps
  }

  task sourcesJar( type: Jar, dependsOn: classes ) {
    from sourceSets.main.allSource
    classifier = 'sources'
  }

  task javadocJar( type: Jar, dependsOn: javadoc ) {
    classifier = 'javadoc'
    from javadoc.destinationDir
  }

  task testsJar( type: Jar, dependsOn: testClasses ) {
    from sourceSets.test.output
    classifier = 'tests'
  }

  task testSourcesJar( type: Jar, dependsOn: classes ) {
    from sourceSets.test.allSource
    classifier = 'test-sources'
  }

  artifacts {
    archives jar
    archives sourcesJar
    archives javadocJar
    archives testsJar
    archives testSourcesJar
    testArtifacts testsJar
    testArtifacts testSourcesJar
  }

  dependencies {
    testCompile group: 'junit', name: 'junit', version: junitVersion
    jarjar group: 'org.ow2.asm', name: 'asm', version: asmVersion
    jarjar group: 'org.ow2.asm', name: 'asm-commons', version: asmVersion
    jarjar group: 'com.googlecode.jarjar', name: 'jarjar', version: jarjarVersion
  }

  uploadArchives {

    repositories.mavenDeployer {
      configuration = configurations.archives

      repository( url: repoUrl ) {
        authentication( userName: repoUserName, password: repoPassword )
      }

      pom.project {
        description 'Lingual is true SQL for Cascading and Apache Hadoop.'
        inceptionYear '2012'
        url 'http://cascading.org/lingual/'
        scm {
          url 'https://github.com/Cascading/lingual.git'
        }
        licenses {
          license {
            name 'The Apache Software License, Version 2.0'
            url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
            distribution 'repo'
          }
        }
      }
    }
  }
}

task javadocAll( type: Javadoc ) {

  ext.copyright = '<i>Copyright &#169; 2007-2013 Concurrent, Inc. All Rights Reserved.</i>'
  title = "Cascading Lingual ${releaseVersion}"

  source project( ":lingual-core" ).sourceSets.main.allJava
  source project( ":lingual-platform" ).sourceSets.main.allJava

  source 'lingual-platform/src/test/java/cascading/lingual/LingualPlatformTestCase.java'
  source 'lingual-platform/src/test/java/cascading/lingual/jdbc/JDBCPlatformTestCase.java'
  source 'lingual-platform/src/test/java/cascading/lingual/jdbc/SimpleSqlPlatformTest.java'

  destinationDir = new File( buildDir, 'javadoc' )

  classpath = files( subprojects.collect { project ->
    project.sourceSets.test.compileClasspath
  } )

  configure( options ) {
    encoding = "UTF8"
    bottom = copyright
    links = [
            'http://docs.concurrentinc.com/cascading/2.2/javadoc/',
            'http://docs.oracle.com/javase/6/docs/api/',
            'http://hadoop.apache.org/common/docs/r1.0.4/api/',
            'http://junit.sourceforge.net/javadoc/'
    ]
  }
}

task javadocDist( type: Copy, dependsOn: javadocAll ) {
  description = "build javadoc"

  from javadocAll.destinationDir
  into "${s3UploadDocs.source}/javadoc/"

  s3UploadDocs.dependsOn javadocDist
}