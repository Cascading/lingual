/*
 * Copyright (c) 2007-2015 Concurrent, Inc. All Rights Reserved.
 *
 * Project and contact information: http://www.cascading.org/
 *
 * This file is part of the Cascading project.
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *     http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */

apply plugin: 'maven'

configurations {
    shaded
    shadedArchives
}

shadowJar {
  baseName = "${project.name}-jdbc"
  classifier = ''
  mergeServiceFiles()
  exclude 'LICENSE'
  exclude 'META-INF/*.DSA'
  exclude 'META-INF/LICENSE*'
  relocate( 'com.', 'cascading.thirdparty.com.' ) {
    exclude( 'com.google.**' )
  }
  relocate( 'net.', 'cascading.thirdparty.net.' ) {
    exclude( 'net.hydromatic.**' )
  }
  relocate( 'org.', 'cascading.thirdparty.org' ) {
    exclude('org.apache.**')
    exclude('org.slf4j.**')
    exclude('org.jgrapht.**')
    exclude('org.xml.**')
    exclude('org.eigenbase.**')
    exclude('org.jets3t.**')
    exclude('org.w3c.**')
  }
}

uploadShadow {
   def deployer = repositories.mavenDeployer {
    configuration = configurations.shadow

    repository( url: repoUrl ) {
       authentication( userName: repoUserName, password: repoPassword )
    }

    pom.project {
      description 'Lingual is true SQL for Cascading and Apache Hadoop.'
      inceptionYear '2012'
      url 'http://cascading.org/lingual/'
      scm {
        url 'https://github.com/Cascading/lingual.git'
      }
      licenses {
        license {
          name 'The Apache Software License, Version 2.0'
          url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
          distribution 'repo'
        }
      }
    }
  }
  deployer.pom.whenConfigured { pom -> pom.dependencies = [] }
}

task installShadowArchives( type: Upload ) {
  configuration = configurations['shadedArchives']
  def installer = repositories.mavenInstaller {
    configuration = configurations.shadedArchives
    pom.project {
      description 'Lingual is true SQL for Cascading and Apache Hadoop.'
      inceptionYear '2012'
      url 'http://cascading.org/lingual/'
      scm {
        url 'https://github.com/Cascading/lingual.git'
      }
      licenses {
        license {
          name 'The Apache Software License, Version 2.0'
          url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
          distribution 'repo'
        }
      }
    }
  }
  installer.pom.whenConfigured { pom -> pom.dependencies = [] }
}


artifacts {
  shadedArchives shadowJar
}

uploadArchives.dependsOn( uploadShadow ) 

install.dependsOn( installShadowArchives )
