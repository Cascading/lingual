/*
 * Copyright (c) 2007-2015 Concurrent, Inc. All Rights Reserved.
 *
 * Project and contact information: http://www.cascading.org/
 *
 * This file is part of the Cascading project.
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *     http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */

import org.apache.tools.ant.filters.ReplaceTokens

evaluationDependsOn( ':lingual-local' )
evaluationDependsOn( ':lingual-hadoop' )
evaluationDependsOn( ':lingual-hadoop2-mr1' )
evaluationDependsOn( ':lingual-hadoop2-tez' )

apply from: "../etc/shadow.gradle"

dependencies {

  provided project( ':lingual-core' ) // provided by platform/jdbc drivers

  compile group: 'net.sf.jopt-simple', name: 'jopt-simple', version: joptVersion
  compile group: 'sqlline', name: 'sqlline', version: sqllineVersion
  compile group: 'org.apache.ivy', name: 'ivy', version: ivyVersion

  runtime group: 'jline', name: 'jline', version: jlineVersion

  provided group: 'org.slf4j', name: 'slf4j-api', version: slf4jVersion

  provided group: 'org.slf4j', name: 'slf4j-api', version: slf4jVersion

  testCompile project( ':lingual-core' ) // provided by platform/jdbc drivers
}

task assembleDist( dependsOn: shadowJar ) {
  dependsOn << project( ":lingual-local" ).tasks[ "shadowJar" ]
  dependsOn << project( ":lingual-hadoop" ).tasks[ "hadoopJar" ]
  dependsOn << project( ":lingual-hadoop" ).tasks[ "shadowJar" ]
  dependsOn << project( ":lingual-hadoop2-mr1" ).tasks[ "hadoopJar" ]
  dependsOn << project( ":lingual-hadoop2-mr1" ).tasks[ "shadowJar" ]
  dependsOn << project( ":lingual-hadoop2-tez" ).tasks[ "hadoopJar" ]
  dependsOn << project( ":lingual-hadoop2-tez" ).tasks[ "shadowJar" ]

  ext.distDir = file( "${buildDir}/dist" )
}

assembleDist << {

  distDir.delete();
  distDir.mkdir();

  copy {
    into "${distDir}/lib"
    from configurations.runtime
    from jar
  }

  copy {
    into "${distDir}/platform/local"
    from project( ":lingual-local" ).shadowJar
  }

  copy {
    into "${distDir}/platform/hadoop"
    from project( ":lingual-hadoop" ).shadowJar
  }

  copy {
    into "${distDir}/platform/hadoop2-mr1"
    from project( ":lingual-hadoop2-mr1" ).shadowJar
  }

  copy {
    into "${distDir}/platform/hadoop2-tez"
    from project( ":lingual-hadoop2-tez" ).shadowJar
  }
  
  copy {
    into distDir
    from( 'src/dist/etc/' ) {
      filter( ReplaceTokens, tokens: [
              'releaseVersion': "${releaseVersion}".toString()
      ] )
    }
  }

  copy {
    fileMode = 0755
    into "${distDir}/bin/"
    from( 'src/dist/bin/' ) {
      rename '\\.sh', ''
      filter( ReplaceTokens, tokens: [
              'location': project.s3Bucket.toString(),
              'majorVersion': majorVersion.toString()
      ] )
    }
  }
}

task packageDist( type: Tar, dependsOn: assembleDist ) {
  description = "package current build, does not run tests"

  destinationDir = file( s3UploadArtifacts.source )
  compression = "GZIP"

  into( baseName ) {
    from assembleDist.distDir
  }

  s3UploadArtifacts.dependsOn packageDist
}

packageDist << {

  copy {
    from( 'src/dist/util/install-lingual-client.sh' ) {
      filter( ReplaceTokens, tokens: [
              'location': project.s3Bucket.toString(),
              'majorVersion': majorVersion.toString()
      ] )
    }
    into s3UploadArtifacts.source
  }

  file( "${s3UploadArtifacts.source}/latest.txt" ).write( "http://${s3UploadArtifacts.destination}${archiveName}" )
}

uploadArchives.enabled = false

platformTest.enabled = false

uploadShadow.enabled = false
installShadowArchives.enabled = false

shadowJar {
  baseName = "${project.name}"
}
