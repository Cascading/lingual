/*
 * Copyright (c) 2007-2013 Concurrent, Inc. All Rights Reserved.
 *
 * Project and contact information: http://www.cascading.org/
 *
 * This file is part of the Cascading project.
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *     http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */

import org.apache.tools.ant.filters.ReplaceTokens

apply plugin: 'application'

evaluationDependsOn( ':lingual-local' )
evaluationDependsOn( ':lingual-hadoop' )

dependencies {

  providedCompile project( ':lingual-core' )

  compile group: 'net.sf.jopt-simple', name: 'jopt-simple', version: '4.4-beta-2'
  compile group: 'sqlline', name: 'sqlline', version: '1_0_2'

  runtime group: 'jline', name: 'jline', version: '1.0'

  providedCompile group: 'log4j', name: 'log4j', version: '1.2.16' // for setting log level as a cli option
  providedCompile group: 'org.slf4j', name: 'slf4j-api', version: '1.6.1'
  providedCompile group: 'org.slf4j', name: 'slf4j-log4j12', version: '1.6.1'
}

task clientDist( dependsOn: jar ) {
  dependsOn << project( ":lingual-local" ).tasks[ "fatJar" ]
  dependsOn << project( ":lingual-hadoop" ).tasks[ "hadoopJar" ]
  dependsOn << project( ":lingual-hadoop" ).tasks[ "fatJar" ]
}

clientDist << {

  def distDir = new File( "${buildDir}/dist" )
  distDir.delete();
  distDir.mkdir();

  copy {
    into "$distDir/lib"
    from configurations.runtime
    from jar.archivePath
  }

  copy {
    into "$distDir/platform/local"
    from project( ":lingual-local" ).fatJar
  }

  copy {
    into "$distDir/platform/hadoop"
    from project( ":lingual-hadoop" ).fatJar
//    from project( ":lingual-hadoop" ).hadoopJar
  }

  copy {
    from( 'src/dist/etc/' ) {
      filter( ReplaceTokens, tokens: [
              'releaseVersion': "${releaseVersion}".toString()
      ] )
    }
    into "${distDir}/"
  }

  copy {
    into "$distDir/docs"
    from 'docs/'
  }

  delete "${distDir}/bin/"

  copy {
    fileMode = 775
    from( 'src/dist/bin/' ) {
      rename '\\.sh', ''
      filter( ReplaceTokens, tokens: [
              'location': "${project.s3Bucket}".toString(),
              'majorVersion': "${majorVersion}".toString()
      ] )
    }
    into "${distDir}/bin/"
  }
}

task clientPackage( type: Tar, dependsOn: clientDist ) {
  description = "package current build, does not run tests"

  def distDir = mkdir( "${buildDir}/dist" )

  destinationDir = buildDir
  baseName = "lingual-client"
  compression = "GZIP"

  into( baseName ) {
    from distDir
  }

  s3Upload.dependsOn clientPackage
  s3Upload.uploadFiles << archivePath
}

clientPackage << {

  copy {
    from( 'src/dist/util/install-lingual-client.sh' ) {
      filter( ReplaceTokens, tokens: [
              'location': "${project.s3Bucket}".toString(),
              'majorVersion': "${majorVersion}".toString()
      ] )
    }
    into buildDir
  }

  s3Upload.uploadFiles << "${buildDir}/install-lingual-client.sh"

  def latestPath = new File( buildDir, 'latest.txt' )
  s3Upload.uploadFiles << latestPath
  latestPath.write( "http://${project.s3Bucket}/${s3Upload.remotePath}${archiveName}" )
}

uploadArchives.enabled = false

platformTest.enabled = false

