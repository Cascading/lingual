/*
 * Copyright (c) 2007-2013 Concurrent, Inc. All Rights Reserved.
 *
 * Project and contact information: http://www.cascading.org/
 *
 * This file is part of the Cascading project.
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *     http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */

def bindVersion = "wip-+"
def bindChanging = false

def linq4jVersion = '0.1'
def linq4jChanging = true // build versioning not implemented yet
def optiqVersion = '0.1'
def optiqChanging = true // build versioning not implemented yet

// set the following property in $root/gradle.properties to use local installs of bind
// systemProp.bind.release.local=true
if( System.properties[ 'bind.release.local' ] )
{
  bindVersion = "wip-dev"
  bindChanging = true
}

// set the following property in $root/gradle.properties to use local installs of optiq/linq4j
// systemProp.optiq.release.local=true
if( System.properties[ 'optiq.release.local' ] )
{
  linq4jVersion = "${linq4jVersion}-SNAPSHOT"
  linq4jChanging = true
  optiqVersion = "${optiqVersion}-SNAPSHOT"
  optiqChanging = true
}

dependencies {
  compile group: 'cascading', name: 'cascading-core', version: cascadingVersion, changing: true
  compile( group: 'cascading', name: 'cascading-bind', version: "1.0.0-${bindVersion}", changing: bindChanging ) {
    exclude group: 'org.slf4j'
  }

  compile( group: 'net.hydromatic', name: 'optiq', version: optiqVersion, changing: optiqChanging ) {
    exclude group: 'xerces'
    exclude group: 'xalan'
    exclude group: 'org.apache.ant'
    exclude group: 'org.jgrapht'
  }

  compile group: 'net.hydromatic', name: 'linq4j', version: linq4jVersion, changing: linq4jChanging
  compile group: 'com.google.guava', name: 'guava', version: '14.0-rc1'
  compile group: 'com.fasterxml.jackson.core', name: 'jackson-databind', version: '2.1.1'
  compile group: 'com.fasterxml.jackson.datatype', name: 'jackson-datatype-guava', version: '2.1.1'

  compile group: 'org.apache.commons', name: 'commons-lang3', version: '3.1'

  providedCompile group: 'org.slf4j', name: 'slf4j-api', version: '1.6.1'
  providedCompile group: 'org.slf4j', name: 'slf4j-log4j12', version: '1.6.1'
  providedCompile group: 'log4j', name: 'log4j', version: '1.2.16'

  testCompile group: 'junit', name: 'junit', version: '4.8.+'
  testCompile group: 'com.google.guava', name: 'guava', version: '13.0.1'

  testCompile group: 'cascading', name: 'cascading-core', version: cascadingVersion, classifier: 'tests', changing: true
  testCompile group: 'cascading', name: 'cascading-platform', version: cascadingVersion, classifier: 'tests', changing: true

  testRuntime group: 'org.slf4j', name: 'slf4j-api', version: '1.6.1'
  testRuntime group: 'org.slf4j', name: 'slf4j-log4j12', version: '1.6.1'
  testRuntime group: 'log4j', name: 'log4j', version: '1.2.16'
}

platformTest.enabled = false
